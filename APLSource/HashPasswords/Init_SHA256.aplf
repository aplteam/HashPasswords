 {r}←{force}Init_SHA256 dummy;extn;⎕USING;filename
⍝ Make the necessary preparations for actually hashing password.\\
⍝ Once initiallized, the function does not run its code again, except when a 1 is passed as `⍺`.
 force←{0<⎕NC ⍵:⍎⍵ ⋄ 0}'force'
 '⍺ must be a Boolean'Assert(⊂force)∊0 1
 :If 9≠⎕NC'SHA256'
 :OrIf force
     :Trap 6
         ⎕USING←',System.Security.Cryptography.Algorithms',('W'=⊃⊃'.'⎕WG'APLVersion')/'.dll'
         SHA256←⎕NEW System.Security.Cryptography.SHA256Managed
     :Else
         'SHA256'⎕NS''
         :If 'Mac'≡3↑⊃'#'⎕WG'APLVersion'
             filename←##.TatinVars.GetFullPath2AssetsFolder,'libconga34ssl64.dylib'
         :Else
             filename←##.TatinVars.GetFullPath2AssetsFolder,'libconga34ssl64.so'
         :EndIf
         'Init'SHA256.⎕NA filename,'|nettle_sha256_init >I1[112]'
         'Update'SHA256.⎕NA filename,'|nettle_sha256_update =I1[112] U8 <T1[]'
         'Digest'SHA256.⎕NA filename,'|nettle_sha256_digest =I1[112] U8 >U1[32]'
         SHA256.⎕FX'digest←ComputeHash data' 'data←⎕UCS⊃data' 'ctx←Init 112' 'ctx←Update ctx(⍴data)data' '(ctx digest)←Digest ctx 32 32'
     :EndTrap
 :EndIf
